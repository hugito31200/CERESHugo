primitive(isSensor(_host)).
primitive(isActuator(_host)).
primitive(physicalAccess(_principal,_host)).
primitive(malicious(_principal)).

primitive(isA(_concreteType, _abstractType)).
primitive(refines(_concreteType,_abstractType)).

primitive(initiallyTriggered(_vulID)).
derived(isTriggered(_vulID)).

derived(accessInfo(_host)).
derived(isCompromised(_device)).
derived(canSniffCommunication(_h1,_h2)).
derived(covertAppropriation(_plc)).

derived(targetAsset(_asset,_property)).


/******************************************************/
/*             Tabling Predicates                     */
/*   All derived predicates should be tabled          */
/******************************************************/

:- table isTriggered/1.
:- table accessInfo/1.
:- table isCompromised/1.
:- table canSniffCommunication/2.
:- table covertAppropriation/1.

/************Vulnerability triggering*******************/

interaction_rule(
  (isTriggered(VulID) :-
  vulExists(_,VulID,_,none,_)
  ),
  rule_desc('Vulnerability triggered from the start', 1)).

interaction_rule(
  (isTriggered(VulID) :-
  vulExists(_,VulID,_,Condition,_),
  vulExists(_,TriggeredVul,_,_,Condition),
  isTriggered(TriggeredVul)
  ),
  rule_desc('Trigger cascading vulnerability', 1)).

interaction_rule(
  (isTriggered(VulID) :-
  vulExists(H1,VulID,SW,hijackcomm,_),
  networkServiceInfo(H1,SW,_,_,_),
  execCode(H2,_),
  hacl(H2,H1,_,_)
  ),
  rule_desc('Hijack communication', 1)).


/*****Denial of Service*********/
/*From 'Simulating and Detecting Attacks of Untrusted Clients in OPC UA Networks'*/
interaction_rule(
  (dos(H) :-
  networkServiceInfo(H,_,opcua,opcua_port,_),
  netAccess(H,_,_)
  ),
  rule_desc('OPC UA Flooding', 1)).

interaction_rule(
  (dos(H) :-
  vulExists(H, VulID, Software, _, dos),
  isTriggered(VulID),
  networkServiceInfo(H,Software,Protocol,Port,_),
  netAccess(H,Protocol,Port)
  ),
  rule_desc('DoS vulnerability exploit', 1)).

/*From 'Denial of service attacks on network-based control systems: impact and mitigation'*/
interaction_rule(
  (dos(H) :-
  execCode(H2,Perm),
  hacl(H2,H,_,_)
  ),
  rule_desc('Local Network Attack', 1)).

/*****Information Disclosure*********/
interaction_rule(
  (accessInfo(H) :-
  vulExists(H,VulID,Software,_,leakinfo),
  isTriggered(VulID),
  networkServiceInfo(H,Software,Protocol,Port,_),
  netAccess(H,Protocol,Port)
  ),
  rule_desc('Extracts info by exploiting vulnerability', 1)).


/*****Covert appropriation*********/
/* From 'A Decoupled Feedback Structure for Covertly Appropriating Networked Control Systems' */

interaction_rule(
  (covertAppropriation(Scada) :-
  isSensor(Sensor),
  isCompromised(Sensor),
  isActuator(Actuator),
  isCompromised(Actuator),
  hacl(Sensor,Scada,_,_),
  hacl(Scada,Actuator,_,_)
  ),
  rule_desc('Covert Appropriation by compromising sensors and actuators', 1)).

interaction_rule(
  (covertAppropriation(PLC) :-
  isSensor(Sensor),
  isActuator(Actuator),
  execCode(PLC,_),
  hacl(Sensor,PLC,_,_),
  hacl(PLC,Actuator,_,_)
  ),
  rule_desc('Covert Appropriation by compromising the PLC', 1)).

interaction_rule(
  (covertAppropriation(PLC) :-
  isSensor(Sensor),
  isActuator(Actuator),
  hacl(Sensor,PLC,_,_),
  canSniffCommunication(Sensor,PLC),
  hacl(PLC,Actuator,_,_),
  canSniffCommunication(PLC,Actuator)
  ),
  rule_desc('Covert Appropriation by sniffing communication btw sensors/actuators and PLC', 1)).


/********************Physically compromising a sensor or an actuator************************************/
interaction_rule(
  (isCompromised(Sensor) :-
  isSensor(Sensor),
  physicalAccess(Principal,Sensor),
  malicious(Principal)
  ),
  rule_desc('Physically compromising a sensor', 1)).

interaction_rule(
  (isCompromised(Actuator) :-
  isActuator(Actuator),
  physicalAccess(Principal,Actuator),
  malicious(Principal)
  ),
  rule_desc('Physically compromising an actuator', 1)).
  

/********************Sniffing on a communication channel************************************/
interaction_rule(
  (canSniffCommunication(Device,Host) :-
  isCompromised(Device),
  hacl(Device,Host,_,_)
  ),
  rule_desc('Compromise source of communication', 1)).

interaction_rule(
  (canSniffCommunication(device,Host) :-
  isCompromised(Device),
  hacl(Host,Device,_,_)
  ),
  rule_desc('Compromise end of communication', 1)).

  interaction_rule(
  (canSniffCommunication(H1,H2) :-
  execCode(H1,_),
  hacl(H1,H2,_,_)
  ),
  rule_desc('Exec code on origin node', 1)).

  interaction_rule(
  (canSniffCommunication(H1,H2) :-
  execCode(H2,_),
  hacl(H1,H2,_,_)
  ),
  rule_desc('Exec code on end node', 1)).


/***********************Compromise of assets*************************/
interaction_rule(
  (targetAsset(HostAsset,availability) :-
  dos(HostAsset)
  ),
  rule_desc('Compromising the availability of an asset', 1)).

interaction_rule(
  (targetAsset(ProgAsset,availability) :-
  dos(Host),
  networkServiceInfo(Host,ProgAsset,_,_,_)
  ),
  rule_desc('Compromising the availability of an asset', 1)).

interaction_rule(
  (targetAsset(HostAsset,integrity) :-
  execCode(HostAsset,_)
  ),
  rule_desc('Compromising the integrity of an asset', 1)).

interaction_rule(
  (targetAsset(ProgAsset,integrity) :-
  execCode(Host,Perm),
  networkServiceInfo(Host,ProgAsset,_,_,Perm)
  ),
  rule_desc('Compromising the integrity of an asset', 1)).

interaction_rule(
  (targetAsset(ProgAsset,integrity) :-
  hasAccount(ProgAsset,Principal,_),
  malicious(Principal)
  ),
  rule_desc('Compromising the integrity of an asset', 1)).


interaction_rule(
  (targetAsset(DeviceAsset,integrity) :-
  covertAppropriation(DeviceAsset)
  ),
  rule_desc('Compromising the integrity of an asset', 1)).

interaction_rule(
  (targetAsset(DeviceAsset,integrity) :-
  physicalAccess(Principal,DeviceAsset),
  malicious(Principal)
  ),
  rule_desc('Compromising the integrity of an asset', 1)).

interaction_rule(
  (targetAsset(HostAsset,confidentiality) :-
  accessInfo(HostAsset)
  ),
  rule_desc('Compromising the confidentiality of an asset', 1)).

interaction_rule(
  (targetAsset(HostAsset,confidentiality) :-
  execCode(HostAsset,_)
  ),
  rule_desc('Compromising the confidentiality of an asset', 1)).

interaction_rule(
  (targetAsset(ProgAsset,confidentiality) :-
  accessInfo(Host),
  networkServiceInfo(Host,ProgAsset,_,_,_)
  ),
  rule_desc('Compromising the confidentiality of an asset', 1)).

interaction_rule(
  (targetAsset(ProgAsset,confidentiality) :-
  execCode(Host,Perm),
  networkServiceInfo(Host,ProgAsset,_,_,Perm)
  ),
  rule_desc('Compromising the confidentiality of an asset', 1)).


/*******************Refinement*********************/
interaction_rule(
  (vulExists(Host,CVE_ID,ConcreteSW,Pre,Post) :-
  isA(ConcreteSW,AbstractSW),
  vulExists(undef,CVE_ID,AbstractSW,Pre,Post),
  networkServiceInfo(Host,ConcreteSW,_,_,_)
  ),
  rule_desc('Direct Refinement', 1)).
/*
  interaction_rule(
  (refines(Concrete,Abstract) :-
  isA(Concrete,Abstract)
  ),
  rule_desc('Direct Refinement', 1)).

  interaction_rule(
  (refines(Concrete,Abstract) :-
  isA(Concrete,Temp),
  refines(Temp,Abstract)
  ),
  rule_desc('Indirect Refinement', 1)). 
*/

:-load_dyn('/tool/mulval/lib/vulList.P').
:-load_dyn('/tool/mulval/lib/elementTypeHierarchy.P').






