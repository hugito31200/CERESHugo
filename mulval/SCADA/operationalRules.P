primitive(isSensor(_host)).
primitive(isActuator(_host)).
primitive(physicalAccess(_principal,_host)).
primitive(malicious(_principal)).

primitive(isA(_concreteType, _abstractType)).
primitive(refines(_concreteType,_abstractType)).

primitive(initiallyTriggered(_vulID)).
derived(isTriggered(_vulID)).

derived(accessInfo(_host)).
derived(dos(_host)).
derived(isCompromised(_device)).
derived(mitm(_h1,_h2)).
derived(covertAppropriation(_h)).

derived(targetAsset(_asset,_property)).
derived(targetAsset(_asset)).




/******************************************************/
/*             Tabling Predicates                     */
/*   All derived predicates should be tabled          */
/******************************************************/

:- table isTriggered/1.
:- table accessInfo/1.
:- table isCompromised/1.
:- table mitm/2.
:- table covertAppropriation/1.

/************Vulnerability triggering*******************/

interaction_rule(
  (isTriggered(VulID) :-
  vulExists(_,VulID,_,none,_)
  ),
  rule_desc('Vulnerability triggered from the start', certain)).

interaction_rule(
  (isTriggered(VulID) :-
  vulExists(_,VulID,_,Condition,_),
  vulExists(_,TriggeredVul,_,_,Condition),
  isTriggered(TriggeredVul)
  ),
  rule_desc('Trigger cascading vulnerability', certain)).

interaction_rule(
  (isTriggered(VulID) :-
  vulExists(H1,VulID,SW,hijackcomm,_),
  cvss(VulID,AC),
  networkServiceInfo(H1,SW,_,_,_),
  execCode(H2,_),
  hacl(H2,H1,_,_)
  ),
  rule_desc('Hijack communication', cvss_ac(AC))).


/*****Denial of Service*********/
/*From 'Simulating and Detecting Attacks of Untrusted Clients in OPC UA Networks'*/
interaction_rule(
  (dos(H) :-
  networkServiceInfo(H,_,opcua,opcua_port,_),
  netAccess(H,opcua,opcua_port)
  ),
  rule_desc('OPC UA Flooding', possible)).

interaction_rule(
  (dos(H) :-
  vulExists(H, VulID, Software, _, dos),
  cvss(VulID,AC),
  isTriggered(VulID),
  networkServiceInfo(H,Software,Protocol,Port,_),
  netAccess(H,Protocol,Port)
  ),
  rule_desc('DoS vulnerability exploit', cvss_ac(AC))).

/*From 'Denial of service attacks on network-based control systems: impact and mitigation'*/
interaction_rule(
  (dos(H) :-
  netAccess(H,_,_)
  ),
  rule_desc('Local Network Attack', unlikely)).

/*****Information Disclosure*********/
interaction_rule(
  (accessInfo(H) :-
  vulExists(H,VulID,Software,_,leakinfo),
  cvss(VulID,AC),
  isTriggered(VulID),
  networkServiceInfo(H,Software,Protocol,Port,_),
  netAccess(H,Protocol,Port)
  ),
  rule_desc('Extracts info by exploiting vulnerability', cvss_ac(AC))).


/*****Covert appropriation*********/
/* From 'A Decoupled Feedback Structure for Covertly Appropriating Networked Control Systems' */

interaction_rule(
  (covertAppropriation(PLC) :-
  isSensor(Sensor),
  isCompromised(Sensor),
  isActuator(Actuator),
  isCompromised(Actuator),
  hacl(Sensor,PLC,_,_),
  hacl(PLC,Actuator,_,_)
  ),
  rule_desc('Covert Appropriation by compromising sensors and actuators', likely)).

interaction_rule(
  (covertAppropriation(PLC) :-
  isSensor(Sensor),
  isActuator(Actuator),
  execCode(PLC,_),
  hacl(Sensor,PLC,_,_),
  hacl(PLC,Actuator,_,_)
  ),
  rule_desc('Covert Appropriation by compromising the PLC', likely)).

interaction_rule(
  (covertAppropriation(PLC) :-
  isSensor(Sensor),
  isActuator(Actuator),
  mitm(Sensor,PLC),
  mitm(PLC,Actuator)
  ),
  rule_desc('Covert Appropriation with MITM', likely)).


/********************Physically compromising a sensor or an actuator************************************/
interaction_rule(
  (isCompromised(Device) :-
  physicalAccess(Principal,Device),
  malicious(Principal)
  ),
  rule_desc('Physically compromising a device', likely)).
  

/********************Man In The Middle************************************/
interaction_rule(
  (mitm(Device,Host) :-
  isCompromised(Device),
  hacl(Device,Host,_,_)
  ),
  rule_desc('CAPEC 94 Man In The Middle', likely)).

interaction_rule(
  (mitm(Host,Device) :-
  isCompromised(Device),
  hacl(Host,Device,_,_)
  ),
  rule_desc('CAPEC 94 Man In The Middle', likely)).

  interaction_rule(
  (mitm(H1,H2) :-
  execCode(H1,_),
  hacl(H1,H2,_,_)
  ),
  rule_desc('CAPEC 94 Man In The Middle', likely)).

  interaction_rule(
  (mitm(H1,H2) :-
  execCode(H2,_),
  hacl(H1,H2,_,_)
  ),
  rule_desc('CAPEC 94 Man In The Middle', likely)).


/***********************Compromise of assets*************************/
interaction_rule(
  (targetAsset(HostAsset,availability) :-
  dos(HostAsset)
  ),
  rule_desc('Compromising the availability of an asset', certain)).

interaction_rule(
  (targetAsset(ProgAsset,availability) :-
  dos(Host),
  networkServiceInfo(Host,ProgAsset,_,_,_)
  ),
  rule_desc('Compromising the availability of an asset', certain)).

interaction_rule(
  (targetAsset(HostAsset,integrity) :-
  execCode(HostAsset,_)
  ),
  rule_desc('Compromising the integrity of an asset', certain)).

interaction_rule(
  (targetAsset(ProgAsset,integrity) :-
  execCode(Host,Perm),
  networkServiceInfo(Host,ProgAsset,_,_,Perm)
  ),
  rule_desc('Compromising the integrity of an asset', certain)).

interaction_rule(
  (targetAsset(ProgAsset,integrity) :-
  hasAccount(ProgAsset,Principal,_),
  malicious(Principal)
  ),
  rule_desc('Compromising the integrity of an asset', likely)).


interaction_rule(
  (targetAsset(DeviceAsset,integrity) :-
  covertAppropriation(DeviceAsset)
  ),
  rule_desc('Covertly appropriating an asset', certain)).

interaction_rule(
  (targetAsset(DeviceAsset,integrity) :-
  physicalAccess(Principal,DeviceAsset),
  malicious(Principal)
  ),
  rule_desc('CAPEC 624 Hardware Fault Injection', unlikely)).

interaction_rule(
  (targetAsset(HostAsset,confidentiality) :-
  accessInfo(HostAsset)
  ),
  rule_desc('Compromising the confidentiality of an asset', certain)).

interaction_rule(
  (targetAsset(HostAsset,confidentiality) :-
  execCode(HostAsset,_)
  ),
  rule_desc('Compromising the confidentiality of an asset', certain)).

interaction_rule(
  (targetAsset(ProgAsset,confidentiality) :-
  accessInfo(Host),
  networkServiceInfo(Host,ProgAsset,_,_,_)
  ),
  rule_desc('Compromising the confidentiality of an asset', certain)).

interaction_rule(
  (targetAsset(ProgAsset,confidentiality) :-
  execCode(Host,Perm),
  networkServiceInfo(Host,ProgAsset,_,_,Perm)
  ),
  rule_desc('Compromising the confidentiality of an asset', certain)).


/***************CAPEC Attacks************************/
interaction_rule(
  (targetAsset(Database,confidentiality) :-
  netAccess(Database,_,_),
  networkServiceInfo(Database,SW,_,_,_),
  refines(SW,sql_sw)
  ),
  rule_desc('CAPEC 66 SQL Injection', likely)).

  interaction_rule(
  (targetAsset(Webserver,integrity) :-
  netAccess(Webserver,_,_),
  networkServiceInfo(Webserver,Webserver_SW,_,_,_),
  refines(Webserver_SW,web_server)
  ),
  rule_desc('CAPEC 101 Server Side Include SSI Injection', possible)).


/*******************Refinement*********************/
interaction_rule(
  (vulExists(Host,CVE_ID,ConcreteSW,Pre,Post) :-
  refines(ConcreteSW,AbstractSW),
  vulExists(undef,CVE_ID,AbstractSW,Pre,Post),
  networkServiceInfo(Host,ConcreteSW,_,_,_)
  ),
  rule_desc('Direct Refinement', certain)).

  interaction_rule(
  (refines(Concrete,Abstract) :-
  isA(Concrete,Abstract)
  ),
  rule_desc('Direct Refinement', certain)).

  interaction_rule(
  (refines(Concrete,Abstract) :-
  isA(Concrete,Temp),
  refines(Temp,Abstract)
  ),
  rule_desc('Indirect Refinement', certain)). 

 interaction_rule(
  (targetAsset(Asset) :-
  targetAsset(Asset,_)
  ),
  rule_desc('Aggregate Target Asset', certain)). 

:-load_dyn('/tool/mulval/lib/vulList.P').






